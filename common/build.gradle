plugins {
    id("java-library")
    id("jacoco")
}

test {
    useJUnitPlatform {
        if (!project.hasProperty('dockerTests')) {
            excludeTags 'docker'
        }
    }
}

jacocoTestReport {
    dependsOn test
}

dependencies {
    testImplementation 'org.slf4j:slf4j-simple:1.7.36'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.13.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation 'org.testcontainers:junit-jupiter:1.21.1'
    testImplementation 'org.mockito:mockito-core:5.18.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.18.0'

    testImplementation 'com.h2database:h2:2.1.214'
    testImplementation 'org.mongodb:mongodb-driver-legacy:5.5.0'
    testImplementation 'org.spongepowered:configurate-yaml:3.7.3'
    testImplementation 'org.spongepowered:configurate-hocon:3.7.3'
    testImplementation 'me.lucko.configurate:configurate-toml:3.7'
    testImplementation 'net.luckperms:rest-api-java-client:0.1'

    api project(':api')
    api 'org.checkerframework:checker-qual:3.12.0'

    compileOnly project(':common:loader-utils')

    compileOnly 'com.mojang:brigadier:1.0.18'
    compileOnly 'org.slf4j:slf4j-api:1.7.30'
    compileOnly 'org.apache.logging.log4j:log4j-api:2.14.0'

    api('net.kyori:adventure-api:4.21.0') {
        exclude(module: 'adventure-bom')
        exclude(module: 'checker-qual')
        exclude(module: 'annotations')
    }

    api('net.kyori:adventure-text-serializer-gson:4.21.0') {
        exclude(module: 'adventure-bom')
        exclude(module: 'adventure-api')
        exclude(module: 'gson')
    }

    api('net.kyori:adventure-text-serializer-legacy:4.21.0') {
        exclude(module: 'adventure-bom')
        exclude(module: 'adventure-api')
    }

    api('net.kyori:adventure-text-serializer-plain:4.21.0') {
        exclude(module: 'adventure-bom')
        exclude(module: 'adventure-api')
    }

    api("net.kyori:adventure-text-minimessage:4.21.0") {
        exclude(module: 'adventure-bom')
        exclude(module: 'adventure-api')
    }

    api('net.kyori:event-api:3.0.0') {
        exclude(module: 'checker-qual')
        exclude(module: 'guava')
    }

    api 'com.google.code.gson:gson:2.7'
    api 'com.google.guava:guava:19.0'

    api 'com.github.ben-manes.caffeine:caffeine:3.2.0'
    api 'com.squareup.okhttp3:okhttp:3.14.9'
    api 'com.squareup.okio:okio:1.17.6'
    api 'net.bytebuddy:byte-buddy:1.15.11'

    api('org.spongepowered:configurate-core:3.7.3') {
        transitive = false
    }
    api('org.spongepowered:configurate-yaml:3.7.3') {
        transitive = false
    }
    api('org.spongepowered:configurate-gson:3.7.3') {
        transitive = false
    }
    api('org.spongepowered:configurate-hocon:3.7.3') {
        transitive = false
    }
    api('me.lucko.configurate:configurate-toml:3.7') {
        transitive = false
    }
    compileOnly 'com.zaxxer:HikariCP:6.3.0'
    compileOnly 'redis.clients:jedis:5.2.0'
    compileOnly 'io.nats:jnats:2.21.1'
    compileOnly 'com.rabbitmq:amqp-client:5.25.0'
    compileOnly 'org.mongodb:mongodb-driver-legacy:5.5.0'
    compileOnly 'org.postgresql:postgresql:42.7.6'
    compileOnly 'org.yaml:snakeyaml:1.33'
    compileOnly 'net.luckperms:rest-api-java-client:0.1'
}


// Only used occasionally for deployment - not needed for normal builds.
javadoc {
    title = 'LuckPerms Minestom Loader (v' + project.ext.apiVersion + ')'
    options.overview = 'javadoc/overview.html'
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
    options.links(
            'https://checkerframework.org/api/',
            'https://www.javadocs.dev/org.jetbrains/annotations/26.0.2/'
    )
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('-since', '5.0,5.1,5.2,5.3,5.4,5.5')

    if (JavaVersion.current() > JavaVersion.VERSION_1_8) {
        options.links.add('https://docs.oracle.com/en/java/javase/21/docs/api/')
    } else {
        options.links.add('https://docs.oracle.com/javase/8/docs/api/')
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            name = 'OneLiteFeatherRepository'

            def releasesRepoUrl = uri('https://repo.onelitefeather.dev/onelitefeather-releases')
            def snapshotsRepoUrl = uri('https://repo.onelitefeather.dev/onelitefeather-snapshots')

            url = (version.toString().contains('BETA')
                    || version.toString().contains('ALPHA')
                    || version.toString().contains('SNAPSHOT')) ? snapshotsRepoUrl : releasesRepoUrl

            credentials(PasswordCredentials) {
                username = System.getenv('ONELITEFEATHER_MAVEN_USERNAME')
                password = System.getenv('ONELITEFEATHER_MAVEN_PASSWORD')
            }

            authentication {
                basic(BasicAuthentication)
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            artifactId = 'common'

            from components.java

            pom {
                name = 'LuckPerms Common'
                description = 'A permissions manager for Minecraft servers.'
                url = 'https://luckperms.net'

                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'lucko'
                        name = 'Luck'
                        url = 'https://lucko.me'
                        email = 'git@lucko.me'
                    }
                }

                scm {
                    connection = 'scm:git:https://github.com/OneLiteFeatherNET/LuckPerms.git'
                    developerConnection = 'scm:git:git@github.com:OneLiteFeatherNET/LuckPerms.git'
                    url = 'https://github.com/OneLiteFeatherNET/LuckPerms'
                }

                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/OneLiteFeatherNET/LuckPerms/issues'
                }
            }
        }
    }
}

